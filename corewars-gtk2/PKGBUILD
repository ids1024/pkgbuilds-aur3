# Maintainer: Ian D. Scott <ian@perebruin.com>
# Maintainer: kfgz <kfgz at interia dot pl>
# Contributor: damir <damir at archlinux dot org>

_pkgname=corewars
pkgname=corewars-gtk2
pkgver=0.9.13
pkgrel=1
pkgdesc="Simulation game where a number of programs try to crash each other"
arch=('i686' 'x86_64')
url="http://sourceforge.net/projects/corewars/"
license=('GPL2')
depends=('gtk2')
source=(${_pkgname}-${pkgver}.tar.gz::http://sourceforge.net/projects/${_pkgname}/files/${_pkgname}%20%28stable%2C%20tar.gz%29/${pkgver}/${_pkgname}-${pkgver}.tar.gz/download corewars-gtk2.patch)
md5sums=('0a0b8067533b83a96488109ee265f664'
         '3c83e65809b3d406fe4722349a43e7bc')

prepare() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  patch -p1 < ../corewars-gtk2.patch
  sed -i 's/LIBS="$LIBS $GTK_LIBS"/LIBS="-lm $LIBS $GTK_LIBS"/' configure.ac
}

build() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  ./configure --prefix=/usr --mandir=/usr/share/man
  make
}

package() {
  cd "${srcdir}/${_pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}
